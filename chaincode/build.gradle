/*
 * SPDX-License-Identifier: Apache-2.0
 */

plugins {
    id 'application'
    id 'checkstyle'
    id 'jacoco'
}

group 'edu.tsp'
version '1.0-SNAPSHOT'

dependencies {
    compileOnly 'org.hyperledger.fabric-chaincode-java:fabric-chaincode-shim:2.+'
    implementation 'com.owlike:genson:1.5'
    testImplementation 'org.hyperledger.fabric-chaincode-java:fabric-chaincode-shim:2.+'
    testImplementation 'org.junit.jupiter:junit-jupiter:5.4.2'
    testImplementation 'org.assertj:assertj-core:3.11.1'
    testImplementation 'org.mockito:mockito-core:2.+'
}

repositories {
    maven {
        url "https://hyperledger.jfrog.io/hyperledger/fabric-maven"
    }
    jcenter()
    maven {
        url 'https://jitpack.io'
    }
}

configurations{
    compileClasspath{
        attributes.attribute(TargetJvmEnvironment.TARGET_JVM_ENVIRONMENT_ATTRIBUTE, objects.named(TargetJvmEnvironment.class, TargetJvmEnvironment.STANDARD_JVM))
    }
}

application {
    mainClass = 'org.hyperledger.fabric.contract.ContractRouter'
}

checkstyle {
    toolVersion = '8.21'
}

afterEvaluate {
    tasks.withType(Checkstyle).configureEach {
        configDirectory.set(file("$projectDir/config/checkstyle"))
        configProperties = [
            'config_loc': "$projectDir/config/checkstyle"
        ]
    }
}

checkstyleMain {
    source ='src/main/java'
}

checkstyleTest {
    source ='src/test/java'
}

jacocoTestReport {
    dependsOn test
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                minimum = 0.0
            }
        }
    }

    finalizedBy jacocoTestReport
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
}

check.dependsOn jacocoTestCoverageVerification
installDist.dependsOn check
